model
{

 Cl <- theta[3]*exp(eta3)
  V1 <- theta[2]*exp(eta2)
  ka <- theta[1]*exp(eta1)
  f_oral <- theta[4]*exp(eta4)
  V2 <- theta[6] * exp(eta5)
  Q <- theta[7] * exp(eta6)
  t_lag <- theta[5]
  
  eta1~dnorm(0, 1/pow(omega[1],2)) # dnorm in JAGS and BUGS uses precision not SD
  eta2~dnorm(0, 1/pow(omega[2],2))
  eta3~dnorm(0, 1/pow(omega[3],2))
  eta4~dnorm(0, 1/pow(omega[4],2))
  eta5~dnorm(0, 1/pow(omega[5],2))
  eta6~dnorm(0, 1/pow(omega[6],2))
  
  k=Cl/V1
  k12 = Q/V1
  k21 = Q/V2
  
  
  beta = 0.5 * (k12 + k21 + k - sqrt((k12 + k21 + k)^2 - 4 * k21 * k))
  alpha = (k21 * k) / beta
  
  A = ka/V1 * (k21-alpha)/((ka-alpha) * (beta-alpha))
  B = ka/V1 * (k21-beta)/((ka-beta) * (alpha-beta))
  

  
  for(t in 1:length(times)){

    for(i in 1:length(amt)) {
      
      a[t, i]<- ifelse((times[t]-dosing_time[i]) <= t_lag,
                             0 , 
                             f_oral*amt[i]*(A*exp(-alpha*(times[t]-dosing_time[i]-t_lag)) + B * exp(-beta*(times[t]-dosing_time[i]-t_lag)) - (A+B)*exp(-ka*(times[t]-dosing_time[i]-t_lag))) ) 
      
    }
    mu[t] <- sum(a[t,])
    
    c[t]~dnorm(mu[t], 1/pow(sigma,2))
  }

}
